//Script info for Map: TE_ufo (Foo Fighter tourney edition) 
//By Tom "Menzel" Haskins (thaskins@snet.net)
//Last edited: AGENT


game_manager
{
	spawn
	{
		// Set scenario information

		wm_mapdescription		"Allies must infiltrate the Foo Fighter base and transmit the secret documents."

		// Set the respawn times

		wm_allied_respawntime		26
		wm_axis_respawntime		32

		wm_set_round_timelimit		12

		// Set the number of objectives for the map

		wm_number_of_objectives		4

		// Axis control all objectives at the start (0 == AXIS, 1 == ALLIED)
		wm_set_objective_status		1	0
		wm_set_objective_status		2	0
		wm_set_objective_status		3	0
		wm_set_objective_status		4	-1
		wm_objective_allied_desc	1	"Primary Objective:**Breach the Base Gates."
		wm_objective_allied_desc	2	"Primary Objective:**Steal the top secret UFO documents."
		wm_objective_allied_desc	3	"Primary Objective:**Get to the Radio Room and transmit the UFO Documents."
		wm_objective_allied_desc	4	"Secondary Objective:**Control the Allied Reinforcement Point."
		wm_objective_axis_desc		1	"Primary Objective:**Defend the Base Gates."
		wm_objective_axis_desc		2	"Primary Objective:**Prevent Allies from Stealing Top Secret UFO Documents."
		wm_objective_axis_desc		3	"Primary Objective:**Stop the Allies from Transmitting the UFO Documents."
		wm_objective_axis_desc		4	"Secondary Objective:**Control the Allied Reinforcement Point."
		wm_overview_image				"video/mp_ufo.roq"
		wm_objective_image			1	"gfx/2d/mp_objectives/ufo_obj1.tga"
		wm_objective_image			2	"gfx/2d/mp_objectives/ufo_obj2.tga"
		wm_objective_image			3	"gfx/2d/mp_objectives/ufo_obj3.tga"
		wm_objective_image			4	"gfx/2d/mp_objectives/checkpoint_obj.tga"

		// SHORT DESCRIPTIONS

		wm_objective_short_axis_desc	1	"Defend the Base Gates"
		wm_objective_short_axis_desc	2	"Defend the UFO Documents"
		wm_objective_short_axis_desc	3	"Stop the Allies from Transmitting"
		wm_objective_short_axis_desc	4	"Hold the Reinforcement Point"

		wm_objective_short_allied_desc	1	"Destroy the Base Gates"
		wm_objective_short_allied_desc	2	"Steal the UFO Documents"
		wm_objective_short_allied_desc	3	"Transmit the UFO Documents"
		wm_objective_short_allied_desc	4	"Capture the Reinforcement Point"

		// Set Defending Team for SW Mode

		wm_set_defending_team	0

		// If the round timer expires, the Axis have won, so set the current winning team
		// Set the round winner:  0 == AXIS, 1 == ALLIED
		wm_setwinner 0

		// Accum #1 will be the state of objective number one
		accum 1 set 0
	}

	trigger objective1
	{
		// Change the objective state internally, so UI can update, etc.
		// Allied takes control of objective #1 (Main Gate)
		wm_set_objective_status		1	1

		// UI pop-up to alert players
		wm_announce	"The Main Gate has been breached!"

	}
	trigger objective1a
	{
		// Change the objective state internally, so UI can update, etc.
		// Allied takes control of objective #1 (South Gate)
		wm_set_objective_status		1	1

		// UI pop-up to alert players
		wm_announce	"The South Gate has been breached!"

	}

	trigger objective2
	{
		// Change the objective state internally, so UI can update, etc.
		// Allied takes control of objective #1
		wm_set_objective_status		3	1

		// Change the variable within the script so that we can check if someone wins the round
		accum 1 set 1

		// UI pop-up to alert players
		wm_announce	"Allies Transmitted the UFO Documents!"
		wait 1500
		// Call function to check if the round has been won
		trigger game_manager checkgame
	}

	trigger axis_object_stolen
	{
		// Change the objective state internally, so UI can update, etc.
		// Allied have stolen the documents (0 == AXIS, 1 == ALLIED)
		wm_set_objective_status		2	1
	}

	trigger axis_object_returned
	{
		// Change the objective state internally, so UI can update, etc.
		// Axis have returned the documents (0 == AXIS, 1 == ALLIED)
		wm_set_objective_status		2	0
	}

	trigger checkgame
	{
		accum 1 abort_if_not_equal 1

		// Set the round winner:  0 == AXIS, 1 == ALLIED
		wm_setwinner 1

		// End the round
		wm_endround
	}

	trigger south_flagblue
	{
		// Change the objective state internally, so UI can update, etc.
		// Allied takes control of checkpoint #1
		wm_set_objective_status		4	1
		wm_allied_respawntime		22

		// Some kind of UI pop-up to alert players
		wm_announce	"Allies Capture The Reinforcement Point!"
	}

	trigger south_flagred
	{
		// Change the objective state internally, so UI can update, etc.
		// Axis takes control of checkpoint #1
		wm_set_objective_status		4	0
		wm_allied_respawntime		26

		// Some kind of UI pop-up to alert players
		wm_announce	"Axis Regain Control of The Reinforcement Point!"
	}

}


allied_obj1 //main gate breached.
{
	spawn
	{
		remove
	}

	death
	{
		trigger game_manager objective1
	}
}

allied_obj1a //South Gate breached
{
	spawn
	{
		remove
	}

	death
	{
		trigger game_manager objective1a
	}
}

allied_obj2 //The UFO documents being transmitted.
{
	spawn
	{
		remove
	}

	death
	{
		trigger game_manager objective2
	}
}

south_flag
{
	spawn
	{
		remove
	}
	trigger axis_capture
	{
		trigger game_manager south_flagred
	}

	trigger allied_capture
	{
		trigger game_manager south_flagblue
	}
}
flag_respawn_base  // flag in tunnel captured
{
	spawn
	{
		remove
	}
}